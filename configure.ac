#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
# vim: set ts=4 sw=4:

AC_PREREQ(2.59)
AC_INIT([fred-mod-eppd], 1.5, [], [fred-mod-eppd])
AC_CONFIG_SRCDIR([mod_eppd.c])

AH_TOP([#include "unconfig.h" /* why this? See comment in unconfig.h */])

# doxygen
DX_HTML_FEATURE(ON)
DX_CHM_FEATURE(OFF)
DX_CHI_FEATURE(OFF)
DX_MAN_FEATURE(OFF)
DX_RTF_FEATURE(OFF)
DX_XML_FEATURE(OFF)
DX_PDF_FEATURE(OFF)
DX_PS_FEATURE(OFF)
DX_INIT_DOXYGEN(fred-mod-eppd, doxy.conf, [doc])

# init automake
AM_INIT_AUTOMAKE([-Wall -Werror])

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_EGREP
AC_PROG_AWK
AC_PROG_INSTALL
AM_PROG_LIBTOOL

# Required byt automake per-targate flags
AM_PROG_CC_C_O

# Check for libxml2 library and header files
AM_PATH_XML2([2.0.0],[],[AC_MSG_ERROR([missing libxml header files])])

# Check for orbit2
AM_PATH_ORBIT2([2.0.0],[],[AC_MSG_ERROR([missing orbit2 header files.])])
AX_PATH_ORBIT2_COSNAME()

# Check for openssl library and header files
AC_CHECK_LIB([ssl], [X509_digest], [], [AC_MSG_ERROR([openssl not found])])
AC_CHECK_HEADER([openssl/ssl.h], [],
				[AC_MSG_ERROR([missing openssl header files])])

# check apxs tool
AX_WITH_APXS([no],[${PATH}:/usr/local/bin:/usr/local/sbin:/usr/sbin:/sbin])
if test "$APXS" = "no"; then
    AC_MSG_ERROR([apxs executable does not exist (use --with-apxs)])
fi
# check for ap_release.h
# if present we have a apache 2.0.x and greater (in lower it's not included)
# syntax of \#define AP_SERVER_MAJORVERSION differ in minor versions
APXS_INCDIR="`${APXS} -q INCLUDEDIR`"
AC_CHECK_FILE([$APXS_INCDIR/ap_release.h],
              [AC_MSG_NOTICE([apache version >=2.0.x found])],
              [AC_MSG_ERROR([apache version >=2.0.x not found.])])

# check apr-config
AC_ARG_WITH([apr-config], [AC_HELP_STRING([[--with-apr-config]],
                          [location of apr-config executable])],
                          [apr_config="$with_val"],
                          [AC_PATH_PROGS([apr_config], 
                          [apr-config apr-0-config apr-1-config], [no],
                          [$PATH:/usr/sbin/:/usr/local/apache2/bin])])
if test "$APR_CONFIG" = "no"; then
    AC_MSG_ERROR([apr-config executable "$APR_CONFIG" does not exist (use --with-apr-config)])
fi
$apr_config --cppflags &> /dev/null
if test "$?" != "0"; then
    AC_MSG_ERROR([$apr_config is not valid apr-config executable])
fi

# set apache flags
APACHE_CFLAGS="`${APXS} -q CFLAGS` -I`${APXS} -q INCLUDEDIR` -I`${apr_config} --includedir`"
APACHE_CPPFLAGS="`${apr_config} --cppflags`"
AC_SUBST(APACHE_CFLAGS)
AC_SUBST(APACHE_CPPFLAGS)

#set apache module path
#hack: replacing lib-part of dir with ${libdir} to support prefix install
APACHE_LEDIR=`${APXS} -q LIBEXECDIR`
APACHE_LIBEXECDIR=${libdir}${APACHE_LEDIR##${APACHE_LEDIR%/*/*}};
AC_SUBST(APACHE_LIBEXECDIR)

# check IDL dir
AC_ARG_WITH([idldir], [AS_HELP_STRING([--with-idldir],
                        [Directory with idl [/usr/share/idl/fred/]])],
                        [AC_SUBST([IDLDIR], [$withval])],
                        [AC_SUBST([IDLDIR], [/usr/share/idl/fred])])

# check IDL file
AC_ARG_WITH([idl], [AS_HELP_STRING([--with-idl],
                        [location of idl file containing EPP interface [IDLDIR/EPP.idl]])],
                        [AC_SUBST([IDL], [$withval])],
                        [AC_SUBST([IDL], [$IDLDIR/EPP.idl])])
AC_CHECK_FILE([$IDL], [], [AC_MSG_ERROR(
                           [idl file "$IDL" does not exist (use --with-idl)])])

# determine prefix of files generated by idl compiler
idlprefix=`basename $IDL .idl`
AC_SUBST([IDLPREFIX], [$idlprefix])

# check schema file
AC_ARG_WITH([schema], [AS_HELP_STRING([--with-schema],
			[xml schema defining EPP protocol [schemas/all-1.4.xsd]])],
			[AC_SUBST([SCHEMA], [$withval])],
			[AC_SUBST([SCHEMA], [$srcdir/schemas/all-1.4.xsd])])
AC_CHECK_FILE([$SCHEMA], [], [AC_MSG_ERROR(
			   [xml schema "$SCHEMA" does not exist (use --with-schema)])])

# check svn revision (if available)
AC_SUBST([SVNREV], [unknown])
AC_MSG_NOTICE([trying to get svn revision])
AC_PATH_PROG([SVN], [svn], [false])
if test "$SVN" != "false"; then
	AC_CHECK_FILE([.svn], [AC_SUBST([SVNREV],
				  [`$SVN info | $EGREP Revision |  $AWK '{ print $2;};'`])])
	AC_MSG_CHECKING([SVN revision])
	AC_MSG_RESULT([$SVNREV])
else
	AC_MSG_CHECKING([SVN revision])
	AC_MSG_RESULT([$SVNREV])
fi
AC_DEFINE_UNQUOTED([SVN_REV], [["]$SVNREV["]], [SVN revision])

# check perf option
AC_ARG_ENABLE([profiling],
			  AS_HELP_STRING([--enable-profiling], [enable simple profiling support]),
			  profiling=$enableval)
if test "$profiling" = "yes"; then
	AC_DEFINE([EPP_PERF], [1], [Profiling support])
fi

# finalization
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_FILES([Makefile
		 fred-mod-eppd.spec])
AC_OUTPUT
